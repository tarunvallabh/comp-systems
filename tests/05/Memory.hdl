// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:

    // Use a DMux to choose between RAM16K or Screen/Keyboard
    DMux(in=load, sel=address[14], a=loadram, b=loadstuff);
    // Use another DMux to choose between Screen and Keyboard
    DMux(in=loadstuff, sel=address[13], a=scr);
    // RAM 16K needs 14 bus lines for address, hence the address is 0..13
    RAM16K(in=in, load=loadram, address=address[0..13], out=ramout);
    // Screen needs 13 bus lines for address, hence the address is 0..12
    Screen(in=in, load=scr, address=address[0..12], out=screenout);
    Keyboard(out=kbdout);
    // Mux16 to select between screen and keyboard output 
    Mux16(a=screenout, b=kbdout, sel=address[13], out=kbdscrout);
    // Mux16 to select between RAM and the combined screen/keyboard output
    Mux16(a=ramout, b=kbdscrout, sel=address[14], out=out);

}